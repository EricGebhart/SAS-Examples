
proc template;
    define tagset tagsets.supermap;
    
        default_event = "decide";

        indent = 2;

        define event initialize;
            trigger set_options;
            trigger documentation;
        end;

        define event options_set;
            /* DEBUG */
            /*
            putlog "Options_Set";
        
            iterate $options;
            do /while _name_;
                putlog _name_ ":" _value_;
                next $options;
            done;
            */
            trigger ini_regexes;
            trigger set_options;
            trigger documentation;
        end;
        
        define event set_options;

            /* DEBUG */
            /*
            iterate $options;
            do /while _name_;
                putlog _name_ ":" _value_;
                next $options;
            done;
            */

            /* set up some defaults */
            set $structure_events "doc proc proc_branch branch leaf output";
            set $table_events "table table_head table_body table_foot";
            set $title_events "system_title system_footer proc_title byline";

            set $variable_list "value label name htmlclass anchor url"; /* some */
            set $variable_ex_list "value label name htmlclass anchor url"; /* some */
            set $configuration_name "default";

            do /if $options['DEBUG_LEVEL'];
                set $debug_level $options['DEBUG_LEVEL'];
                eval $debug_level inputn($debug_level, '1.');
                putlog "DEBUG" ": " $debug_level;
            else;
                eval $debug_level 0;
            done;
            
            do /if $options['CONFIGURATION_NAME'];
                set $configuration_name $options['CONFIGURATION_NAME'];
            done;

            do /if $options['CONFIGURATION_FILE'];
                set $configuration_file $options['CONFIGURATION_FILE'];
                trigger read_config_ini;
            else;
                unset $configuration_file;
            done;

            /* DEBUG */
            /* putlog "OPTION" $options['BASIC_VERBOSITY'];*/
            do /if $options['BASIC_VERBOSITY'];
                /* DEBUG */
                /* putlog "OPTION" $options['BASIC_VERBOSITY'];*/
                set $basic_event_verbosity $options['BASIC_VERBOSITY'];
            else;
                set $basic_event_verbosity "some"; /* None Some or All */
            done;

            set $variable_list "value label name"  /if cmp($basic_event_verbosity, 'few');
            
            do /if $options['EXTRA_VERBOSITY'];
                set $extra_event_verbosity $options['EXTRA_VERBOSITY'];
            else;
                set $extra_event_verbosity "some"; /* None Some or All */
            done;
        
            set $variable_ex_list "value label name"  /if cmp($extra_event_verbosity, 'few');
            
            do /if $options['VARIABLES'];
                set $variable_list $options['VARIABLES'];
                set $extra_event_verbosity "Some"; /* None Some or All */
            done;

            do /if $options['EXTRA_VARIABLES'];
                set $variable_list $options['EXTRA_VARIABLES'];
                set $extra_event_verbosity "Some"; /* None Some or All */
            done;

            do /if $options['BASIC_EVENTS'];
                set $basic_events $options['BASIC_EVENTS'];
            else;
                set $basic_events $table_events $structure_events;
            done;
                
            do /if $options['EXTRA_EVENTS'];
                set $extra_events $options['EXTRA_EVENTS'];
            else;
                set $extra_events $title_events;
            done;
                
            do /if $options['VALUE_MATCH'];
                set $value_match $options['VALUE_MATCH'];
                eval $value_re prxparse($value_match);
            else;
                unset $value_match;
            done;
                
            do /if $options['LABEL_MATCH'];
                set $label_match $options['LABEL_MATCH'];
                eval $label_re prxparse($value_match);
            else;
                unset $label_match;
            done;
            
            do /if $options['MARKUP_TAGS'];
                set $markup_tags $options['MARKUP_TAGS'];
            else;
                set $markup_tags "|<|>|</|>|[|]|[/|]|/|";
            done;

            trigger set_markup_tags;

            putlog "BDEST" ":" dest_file;
            trigger write_ini;
            
        end;

        define event set_markup_tags;
            set $delim substr($markup_tags, 1,1);
            set $basic_start_beg scan($markup_tags, 1, $delim);
            set $basic_start_end scan($markup_tags, 2, $delim);
            set $basic_finish_beg scan($markup_tags, 3, $delim);
            set $basic_finish_end scan($markup_tags, 4, $delim);
            set $extended_start_beg scan($markup_tags, 5, $delim);
            set $extended_start_end scan($markup_tags, 6, $delim);
            set $extended_finish_beg scan($markup_tags, 7, $delim);
            set $extended_finish_end scan($markup_tags, 8, $delim);
            set $empty_tag scan($markup_tags, 9, $delim);
            do /if $debug_level >= 1;
                putlog "Basic Start Tag:     " $basic_start_beg "TAG" $basic_start_end;
                putlog "Basic Finish Tag:    " $basic_finish_beg "TAG" $basic_finish_end;
                putlog "Basic Empty Tag:     " $basic_start_beg "TAG" $empty_tag $basic_start_end;

                putlog "Extended Start Tag:  " $extended_start_beg "TAG" $extended_start_end;
                putlog "Extended Finish Tag: " $extended_finish_beg "TAG" $extended_finish_end;
                putlog "Extended Empty Tag:  " $extended_start_beg "TAG" $empty_tag $extended_start_end;
            done;
        end;                              
        

        define event documentation;
            trigger help /if cmp($options['DOC'], 'help');
            trigger settings /if cmp($options['DOC'], 'settings');
        end;
        
        define event settings;
            putlog "  Basic_Events: " $basic_events;
            putlog "  Extra_Events: " $extra_events;
            putlog "  Basic_Verbosity: " $basic_event_verbosity;
            putlog "  Extra_Verbosity: " $extra_event_verbosity;
            putlog "  Value_Match: "  $value_match;
            putlog "  Label_Match: "  $label_match;
            putlog "  Configuration_Name: "  $configuration_name;
            putlog "  Configuration_File: "  $configuration_file;
            putlog "  Debug Level: "  $debug_level;
            putlog "  Markup_Tags: "  $markup_tags;
        end;
    
        define event help;
            putlog "=============================================================================="; 
            putlog "This is help for the supermap tagset."; 
            putlog " "; 
            putlog "Purpose: This tagset is for helping with the exploration of Tagset events and";
            putlog "         their values"; 
            putlog " ";
            putlog 'Example Usage:';
            putlog " ";
            putlog '        ods tagsets.supermap file="test.xml" data="test.ini"';
            putlog '            options(extra_events="system_title system_footer help="doc");';
            putlog " ";
            putlog "Options:";
            putlog " ";
            putlog "        Doc: ";
            putlog "             Description:  Prints this help text if the value is set to 'help'.";
            putlog "                           Prints the current option settings if set to 'settings.'";
            putlog " ";
            putlog "             Possible Values: help, settings";
            putlog "             Default value: " $table_events " " $structure_events;
            putlog "             Current value: " $basic_events;
            putlog " ";
            putlog "        Basic_events: ";
            putlog "             Description:  Determines which events will be shown in a basic";
            putlog "                           way with standard XML tag format and a limited";
            putlog "                           number of attributes.";
            putlog "                           Names must be in lower case for them to match.";
            putlog " ";
            putlog "             Possible Values: Any list of event names or All";
            putlog "             Default value: " $table_events " " $structure_events;
            putlog "             Current value: " $basic_events;
            putlog " ";
            putlog "        Extra_events: ";
            putlog "             Description:  Determines which events will be shown in addition";
            putlog "                           to the basic events.  These events are formatted with";
            putlog "                           Names must be in lower case for them to match.";
            putlog "                           []'s and the names are in uppercase. By default the";
            putlog "                           attributes shown will be the same as the basic";
            putlog "                           events.  This can be changed with the verbosity";
            putlog "                           options.";
            putlog " ";
            putlog "             Possible Values: Any list of event names or All";
            putlog "             Default value: " $title_events;
            putlog "             Current value: " $extra_events;
            putlog " ";
            putlog "        Basic_Verbosity: ";
            putlog "             Description:  Determines which attributes will be shown for the";
            putlog "                           basic events.";
            putlog " ";
            putlog "             Possible Values: None, Some, Few, or All.";
            putlog "             Default value:   Some"; 
            putlog "             Current value: " $basic_event_verbosity;
            putlog " ";
            putlog "        Variables: ";
            putlog "             Description:  Determines which attributes will be shown for the";
            putlog "                           basic events.";
            putlog " ";
            putlog "             Possible Values: A space delimited list of variable names.";
            putlog "             Default value: "; 
            putlog "             Current value: " $variable_list;
            putlog " ";
            putlog "        Extra_Verbosity: ";
            putlog "             Description:  Determines which attributes will be shown for the";
            putlog "                           extra events.";
            putlog " ";
            putlog "             Possible Values: None, Some, Few, or All.";
            putlog "             Default value:   Some"; 
            putlog "             Current value: " $extra_event_verbosity;
            putlog " ";
            putlog "        Extra_Variables: ";
            putlog "             Description:  Determines which attributes will be shown for the";
            putlog "                           extra events.";
            putlog " ";
            putlog "             Possible Values: A space delimited list of variable names.";
            putlog "             Default value: "; 
            putlog "             Current value: " $variable_list;
            putlog " ";
            putlog "        Value_Match: ";
            putlog "             Description:  Uses the contents of the value event variable to";
            putlog "                           determine which events to display.";
            putlog " ";
            putlog "             Possible Values: A Perl regular expression.";
            putlog "             Default value:   "; 
            putlog "             Current value: " $value_match;
            putlog " ";
            putlog "        Label_Match: ";
            putlog "             Description:  Uses the contents of the label event variable to";
            putlog "                           determine which events to display.";
            putlog " ";
            putlog "             Possible Values: A Perl regular expression.";
            putlog "             Default value:   "; 
            putlog "             Current value: " $label_match;
            putlog " ";
            putlog "        Configuration_Name: ";
            putlog "             Description:  Name of the configuration to read or write";
            putlog "                           in the .ini file.";
            putlog " ";
            putlog "             Possible Values: Any reasonable string.";
            putlog "             Default value:   default"; 
            putlog "             Current value: " $configuration_name;
            putlog " ";
            putlog "        Configuration_File: ";
            putlog "             Description:  Name of the configuration file to read.";
            putlog "                           This is a .ini formatted file as written";
            putlog "                           to the data file if one is given";
            putlog "                           If given, the options for the configuration";
            putlog "                           will be loaded on top of any options given on the";
            putlog "                           ods statement.  A file may contain more than one";
            putlog "                           configuration section.  Only the first section that";
            putlog "                           matches the configuration name will be loaded.";
            putlog " ";
            putlog "             Possible Values: A valid file name.";
            putlog "             Default value:   "; 
            putlog "             Current value: " $configuration_file;
            putlog " ";
            putlog "        Debug_Level: ";
            putlog "             Description:  Determine what level of debugging information should";
            putlog "                           be printed to the log.  Higher numbers cause more";
            putlog "                           information to be printed.";
            putlog " ";
            putlog "             Possible Values: 0,1,2,3,4";
            putlog "             Default value:   "; 
            putlog "             Current value: " $debug_level;
            putlog " ";
            putlog "=============================================================================="; 
        end;
    
        define event decide;
            start:
                unset $match;
            
                set $match 'basic' /if cmp($basic_events, 'all');
                set $match 'basic' /if contains($basic_events, event_name);

                set $match 'basic_plus' /if cmp($extra_events, 'all');
                set $match 'basic_plus' /if contains($extra_events, event_name);

                /* DEBUG */
                /*
                do/if cmp(event_name, "table");
                    putlog "Table Event" ":" $match;
                    putlog "extra_events" ":" $extra_events;
                done;
                */

                trigger attribute_matching;
                
                /* push the match onto the stack */
                set $event_stack[] $match;
                
                trigger basic /breakif cmp($match, 'basic');
                trigger basic_plus /breakif cmp($match, 'basic_plus');
                    
            finish:
 
                /* pop the match off the stack */
                set $match $event_stack[-1];
                unset $event_stack[-1];

                trigger basic /breakif cmp($match, 'basic');
                trigger basic_plus /breakif cmp($match, 'basic_plus');
        end;


        define event debug_putall;
             putlog _name_ ": " _value_ ;
        end;
                    

        define event attribute_matching;
            do /if $match;
                /* DEBUG */
                do /if $debug_level >= 4;
                    putlog "BEFORE ATTRIBUTE MATCHING:  MEM Variables";
                    dovars mem debug_putall;
                done;
                    

                do /if any($value_match, $label_match);
                    unset $regex_match;

                    do /if $value_match;

                        /* DEBUG */
                        do /if $debug_level >= 3;
                            putlog "MATCHING on VALUE: ";
                            eval $foo prxmatch($value_re, value); 
                            putlog "MATCH:" $foo  " Value:" value; 
                        done;
                            
                        do /if prxmatch($value_re, value);
                            set $regex_match "True";
                        done;
                    done;

                    do /if $label_match;
                        /* DEBUG */
                        do /if $debug_level >= 3;
                            putlog "MATCHING on VALUE: ";
                            eval $foo prxmatch($label_re, label); 
                            putlog "MATCH:" $foo  " Value:" label; 
                        done;

                        do /if prxmatch($label_re, label);
                            set $regex_match "True";
                        done;
                    done;

                    /* DEBUG */
                    do /if $debug_level >= 4;
                        putlog "AFTER ATTRIBUTE MATCHING:  MEM Variables";
                        dovars mem debug_putall;
                    done;

                    set $match "False" /if ^$regex_match;

                done;
            else;
                set $match "False" /if ^$regex_match;
            done;
        end;

        define event basic;
            start:
                put $basic_start_beg event_name; 
                do /if cmp($basic_event_verbosity, "all");
                    trigger put_all_vars;
                else;
                    set $filter_variable_list $variable_list;
                    trigger put_some_vars;
                done;
                    
                put $empty_tag / if empty;
                put $basic_start_end nl;
                break / if empty;
                ndent;
            finish:
                break / if empty;
                xdent;
                put $basic_finish_beg event_name $basic_finish_end nl; 
        end;

        define event basic_plus;
            start:
                put $extended_start_beg upcase(event_name); 
                
                do /if cmp($extra_event_verbosity, "all");
                    trigger put_all_vars;
                else;
                    set $filter_variable_list $variable_ex_list;
                    trigger put_some_ex_vars;
                done;
                
                put $empty_tag / if empty;
                put $extended_start_end nl;
                break / if empty;
                ndent;
            finish:
                break / if empty;
                xdent;
                put $extended_finish_beg upcase(event_name) $extended_finish_end nl; 
        end;

    
        define event put_all_vars;
            putvars event " " _name_ '="' _value_ '"';
            putvars style " " _name_ '="' _value_ '"';
            putvars mem   " " _name_ '="' _value_ '"';
        end;

        define event put_some_vars;
            dovars event filter_vars;
            dovars style filter_vars;
            dovars mem   filter_vars;
        end;
            

        define event filter_vars;
            put " " _name_ "=" _value_ /if contains($filter_variable_list, _name_); 
        end;
        
/*
        define event put_some_vars;
            putq " value=" value;
            putq " label=" label;
            putq " name="  name;
            putq " htmlclass=" htmlclass;
            putq " anchor=" anchor;
            putq " url="  url;
        end;
     
        define event put_few_vars;
            putq " value=" value;
            putq " label=" label;
            putq " name="  name;
        end;
*/


        define event write_ini;
            file=data;
 
            /*---------------------------------------------------------------eric-*/
            /*-- Only write a configuration once.  If the name changes          --*/
            /*-- it's ok to write it again. It doesn't cover all possiblities   --*/
            /*-- but it should be good enough.                                  --*/
            /*------------------------------------------------------------11Feb05-*/
            break /if ^cmp(dest_file, 'data');
            
            break /if cmp($ini_written, $configuration_name);
            set $ini_written $configuration_name;

            putlog "DEST" ":" dest_file;

            put '[' $Configuration_name ']' nl;
            
            put "Tagset_name =" tagset  nl;
            
            iterate $options;

            do /while _name_;
                put _name_ ' = ' _value_ nl;
                next $options;
            done;
            
            put nl nl;
            
        end;    

        define event ini_regexes;
    
            /* not very accurate but good enough */
/*
            set $section_pattern "/^\\[./";
            eval $section_re prxparse($section_pattern);
            putlog "Section_re" ": " $section_re;

            set $config_name_pattern "/^\[" $configuration_name "\]/";
            eval $config_name_re prxparse($config_name_pattern);
*/

        end;


        define event read_config_ini;
            set $read_file $configuration_file;
            putlog "READING configuration_file" ":" $configuration_file;
            trigger readfile;

            do /if $debug_level >= 1;
                putlog "OPTIONS LOADED from " ":" $configuration_file " : " $configuration_name;
                iterate $options;
                do /while _name_;
                    putlog _name_ " : " _value_;
                    next $options;
                done;
            done;
        end;

        /*---------------------------------------------------------------eric-*/
        /*-- Look for a section that matches the configuration name.        --*/
        /*-- Once found, read the variable in and load them into            --*/
        /*-- the options array.                                             --*/
        /*--                                                                --*/
        /*-- If another section is encountered quit scanning                --*/
        /*-- for options.                                                   --*/
        /*------------------------------------------------------------11Feb05-*/
        define event process_data;

        putlog "Process DATA" ":" $done_reading_section;
        
            break /if $done_reading_section;

            do /if $debug_level >= 2;
                putlog "LOOKING [" $configuration_name "]" " " $record ;
                eval $foo prxmatch($section_re, $record);
                putlog "Match " $section_pattern ":" $foo;
            done;
    
            
/*
            do /if prxmatch($section_re, $record);
*/
            set $record_start substr($record, 1,1);
            
            do /if cmp('[', $record_start);
                putlog "Match Section";
                
                /*do /if prxmatch($config_name_re, $record);*/

                set $config_name_pattern "[" $configuration_name "]";
                Putlog "NAME" $config_name_pattern ":" $record;
                do /if cmp($config_name_pattern, $record);
                    putlog "Match Section Name";
                    set $reading_section "True";
                else;
                    putlog "No Match Section Name";
                    set $done_reading_section "True" /if $reading_section;
                    unset $reading_section;
                done;

            else /if $reading_section;

                do /if $debug_level >= 2;
                    putlog "LOADING [" $configuration_name "]" " " $record ;
                done;

                set $key scan($record, 1, '=');
                set $value scan($record, 2, '=');

                set $key strip($key);
                set $value strip($value);
                
                set $options[$key] $value;
            done;
                

        end;
        
        define event readfile;

            /*---------------------------------------------------eric-*/
            /*-- Set up the file and open it.                       --*/
            /*------------------------------------------------13Jun03-*/

            set $filrf "myfile";
            eval $rc filename($filrf, $read_file);

            do /if $debug_level >= 5;
                putlog "File Name" ":" $rc " : " $read_file;
            done;

            eval $fid fopen($filrf);

            do /if $debug_level >= 5;
                putlog "File ID" ":" $fid;
            done;
                
            
            /*---------------------------------------------------eric-*/
            /*-- datastep functions  will bind directly to the      --*/
            /*-- variable space as it exists.                       --*/
            /*--                                                    --*/
            /*-- Tagset variables are not like datastep             --*/
            /*-- variables but we can create a big one full         --*/
            /*-- of spaces and let the functions write to it.       --*/
            /*--                                                    --*/
            /*-- This creates a variable that is 200 spaces so      --*/
            /*-- that the function can write directly to the        --*/
            /*-- memory location held by the variable.              --*/
            /*-- in VI, 200i<space>                                 --*/
            /*------------------------------------------------27Jun03-*/
            set $file_record  "

                                                               ";

            /*---------------------------------------------------eric-*/
            /*-- Loop over the records in the file                  --*/
            /*------------------------------------------------13Jun03-*/
            do /if $fid > 0 ;

                do /while fread($fid) = 0;

                    set $rc fget($fid,$file_record ,200);

                    do /if $debug_level >= 5;
                        putlog 'Fget' ':' $rc 'Record' ':' $file_record;
                    done;

                    set $record trim($file_record);

                    trigger process_data;
                    
                    /* trimn to get rid of the spaces at the end. */
                    /*put trimn($file_record ) nl;*/

                done;
            done;

           /*-----------------------------------------------------eric-*/
           /*-- close up the file.  set works fine for this.         --*/
           /*--------------------------------------------------13Jun03-*/

            set $rc close($fid);
            set $rc filename($filrf);

        end;

    end;
run;

options obs=2;

ods tagsets.supermap file="map.xml" data="map.ini";

proc print data=sashelp.class;
run;

ods _all_ close;

    
ods tagsets.supermap file="map_b.xml" data="map_b.ini" 
                     options(basic_events="data" 
                             Extra_events="table" );

ods tagsets.supermap options(doc="settings");

proc print data=sashelp.class;
run;

ods _all_ close;

ods tagsets.supermap file="map_c.xml" data="map_c.ini"
     options(basic_events="data" 
             Extra_events="table" 
             basic_verbosity="few");

proc print data=sashelp.class;
run;

ods _all_ close;

ods tagsets.supermap file="map_d.xml" data="map_d.ini" 
     options(basic_events="data" 
             Extra_events="table" 
             basic_verbosity="few" 
             value_match="/Alfred/" 
             doc="settings");

proc print data=sashelp.class;
run;

ods _all_ close;

ods tagsets.supermap file="map_e.xml" options(configuration_file='map_d.ini' debug_level='2');

proc print data=sashelp.class;
run;


ods _all_ close;
    
 
